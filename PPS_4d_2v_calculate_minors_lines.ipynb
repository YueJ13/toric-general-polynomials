{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "5731b47c-f07b-49e5-8d1b-20df28ce74b1",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Defining c40, c31, c22, c13, c04, g11, g12, g21, g22\n",
      "Defining x1, x2\n",
      "Defining x1, x2, c40, c31, c22, c13, c04, g11, g12, g21, g22\n",
      "Defining x1, x2, c40, c31, c22, c13, c04, g11, g12, g21, g22\n",
      "Defining x1, x2, c40, c31, c22, c13, c04, g11, g12, g21, g22\n",
      "Defining x1, x2, c40, c31, c22, c13, c04, g11, g12, g21, g22\n",
      "Defining x1, x2, c40, c31, c22, c13, c04, g11, g12, g21, g22\n",
      "Defining x1, x2, c40, c31, c22, c13, c04, g11, g12, g21, g22\n",
      "Defining x1, x2, c40, c31, c22, c13, c04, g11, g12, g21, g22\n",
      "Defining x1, x2, c40, c31, c22, c13, c04, g11, g12, g21, g22\n",
      "\n",
      "matrix A:\n",
      "\n",
      "[    0   c13     0 4*c04   c04]\n",
      "[  c13 2*c22 4*c04 3*c13   c13]\n",
      "[2*c22 3*c31 3*c13 2*c22   c22]\n",
      "[3*c31 4*c40 2*c22   c31   c31]\n",
      "[4*c40     0   c31     0   c40]\n"
     ]
    }
   ],
   "source": [
    "load('Generatematrix.sage')\n",
    "n_vars = 2\n",
    "d = 4\n",
    "var = ['x%i' % i for i in range(1, n_vars + 1)]\n",
    "indexlist = IntegerVectors(d, n_vars)\n",
    "para1 = ['c%s' % ''.join(map(str, i)) for i in indexlist]\n",
    "para = para1 + ['g%i%i' % (i, j) for i in range(1, n_vars + 1) for j in range(1, n_vars + 1)]\n",
    "C = PolynomialRing(QQ, para)\n",
    "C.inject_variables()\n",
    "D = PolynomialRing(C, var)\n",
    "D.inject_variables()\n",
    "g = matrix(n_vars, n_vars, C.gens()[len(IntegerVectors(d, n_vars)):])\n",
    "gens = []\n",
    "gens = [sum([C.gens()[j]*prod([D.gens()[i]^indexlist[j][i] for i in range(0,n_vars)]) for j in range(0,len(IntegerVectors(d, n_vars)))])]\n",
    "# augmented coefficient matrix A\n",
    "A = symmalg_homo(gens, g, n_vars, d)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b23efaa0-1e4d-43fe-a56f-1063e4f9ee04",
   "metadata": {},
   "outputs": [],
   "source": [
    "# size of A: 5*5"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 81,
   "id": "5e4e72f4-0043-4872-9504-3600a2659b85",
   "metadata": {},
   "outputs": [],
   "source": [
    "#function: calculate the generators of minors\n",
    "def generate_the_factors(k, para1):\n",
    "    R = PolynomialRing(QQ, para1, order='degrevlex')\n",
    "    R.inject_variables()\n",
    "    I = ideal(A.minors(k))\n",
    "    if I == 0:\n",
    "        return 0\n",
    "    else:\n",
    "        g = I.groebner_basis()\n",
    "        #following part turn c_ij*(c_12-c_31) to (c_12-c_31). \n",
    "        #We can simplify like this under the assumption that the coefficients are non-zero.\n",
    "        factored_gens = [i.factor() for i in g]\n",
    "        unique_factors = set()\n",
    "        for factored in factored_gens:\n",
    "            for factor, power in factored:\n",
    "                if not factor.is_monomial():\n",
    "                    unique_factors.add(factor)\n",
    "        result = set()\n",
    "        for element in unique_factors:\n",
    "            if -element not in result:\n",
    "                result.add(element)\n",
    "\n",
    "        ans = R.ideal(list(result)).groebner_basis()\n",
    "        print(\"Dimension: \", R.ideal(list(result)).dimension())  \n",
    "        print(f\"{len(ans)} generators: \")\n",
    "    return ans"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "2d5810db-89bb-4d77-9dfc-29669d5d4731",
   "metadata": {},
   "outputs": [],
   "source": [
    "# 5*5 minors of A "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 82,
   "id": "713455ff-b82b-4582-8246-8f1415862f14",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Defining c40, c31, c22, c13, c04\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "0"
      ]
     },
     "execution_count": 82,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "generate_the_factors(5, para1)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 11,
   "id": "22518733-1f9a-40c8-9c27-1afd394c48ac",
   "metadata": {},
   "outputs": [],
   "source": [
    "# 4*4 minors of A "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 83,
   "id": "fef37c61-8636-4e3e-ae0b-93f85e70b596",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Defining c40, c31, c22, c13, c04\n",
      "Dimension:  3\n",
      "5 generators: \n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "[c31^2*c22^2 - 4*c40*c22^3 - 3*c31^3*c13 + 14*c40*c31*c22*c13 - 18*c40^2*c13^2 - 6*c40*c31^2*c04 + 16*c40^2*c22*c04,\n",
       " c31^2*c22*c13 - 4*c40*c22^2*c13 + 3*c40*c31*c13^2 - 9*c31^3*c04 + 32*c40*c31*c22*c04 - 48*c40^2*c13*c04,\n",
       " c31^2*c13^2 - 3*c40*c22*c13^2 - 3*c31^2*c22*c04 + 8*c40*c22^2*c04 + 4*c40*c31*c13*c04 - 32*c40^2*c04^2,\n",
       " c31*c22*c13^2 - 9*c40*c13^3 - 4*c31*c22^2*c04 + 3*c31^2*c13*c04 + 32*c40*c22*c13*c04 - 48*c40*c31*c04^2,\n",
       " c22^2*c13^2 - 3*c31*c13^3 - 4*c22^3*c04 + 14*c31*c22*c13*c04 - 6*c40*c13^2*c04 - 18*c31^2*c04^2 + 16*c40*c22*c04^2]"
      ]
     },
     "execution_count": 83,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "generate_the_factors(4, para1)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "02598201-f150-4405-b281-f8d7dd153ddb",
   "metadata": {},
   "outputs": [],
   "source": [
    "# 3 * 3 minors of A"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 84,
   "id": "ebe84b9e-9645-48dc-8a7f-08effd061f24",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Defining c40, c31, c22, c13, c04\n",
      "Dimension:  2\n",
      "6 generators: \n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "[c31^2 - 8/3*c40*c22,\n",
       " c31*c22 - 6*c40*c13,\n",
       " c22^2 - 36*c40*c04,\n",
       " c31*c13 - 16*c40*c04,\n",
       " c22*c13 - 6*c31*c04,\n",
       " c13^2 - 8/3*c22*c04]"
      ]
     },
     "execution_count": 84,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "generate_the_factors(3, para1)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "145e8fd9-f689-49fc-b225-9b4070947e20",
   "metadata": {},
   "outputs": [],
   "source": [
    "# 2*2 minors"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 85,
   "id": "3c482dd0-7aed-4a8e-a496-cbf59ca13f83",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Defining c40, c31, c22, c13, c04\n",
      "Dimension:  5\n",
      "1 generators: \n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "[0]"
      ]
     },
     "execution_count": 85,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "generate_the_factors(2, para1)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c64f2ae1-3d31-4d01-8cc4-ba86dadba720",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Following are the conditions for certain number of lines"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7c0f28c1-0c75-4bf9-88c0-a63d89a16743",
   "metadata": {},
   "outputs": [],
   "source": [
    "# 1 line: the variety in parameter space quals to V(3*3 minors of A)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 86,
   "id": "5501501d-56c8-4f15-91c3-ec77022a82b9",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Defining x1, x2, c40, c31, c22, c13, c04, w1, w2\n",
      "6 generators: \n",
      "3*c13^2 - 8*c22*c04\n",
      "c22*c13 - 6*c31*c04\n",
      "c31*c13 - 16*c40*c04\n",
      "c22^2 - 36*c40*c04\n",
      "c31*c22 - 6*c40*c13\n",
      "3*c31^2 - 8*c40*c22\n"
     ]
    }
   ],
   "source": [
    "R = PolynomialRing(QQ, var+para1+['w1']+['w2'])\n",
    "R.inject_variables()\n",
    "f2 = expand(w1*x1-w2*x2)^4\n",
    "f1 = c40*x1^4 + c31*x1^3*x2 + c22*x1^2*x2^2 + c13*x1*x2^3 + c04*x2^4\n",
    "monomials = monomial_generator(R.gens()[:n_vars], d)\n",
    "genel = [f1.coefficient(i)-f2.coefficient(i) for i in monomials]\n",
    "I2 = ideal(genel)\n",
    "elim_ideal = I2.elimination_ideal([w1,w2])\n",
    "num_gens = len(elim_ideal.gens())\n",
    "print(f\"{num_gens} generators: \")\n",
    "for gen in elim_ideal.gens():\n",
    "    print(gen)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "43fc430e-e983-4192-be59-bb3833f6a6bb",
   "metadata": {},
   "outputs": [],
   "source": [
    "# 2 lines: two ways to decompose degree 4: 3+1 and 2+2"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 87,
   "id": "4305a828-c7b2-4594-b1e9-1c9df686110a",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Defining x1, x2, c40, c31, c22, c13, c04, w1, w2, v1, v2\n",
      "3 generators: \n",
      "c22^2 - 3*c31*c13 + 12*c40*c04\n",
      "c31*c22*c13 - 9*c40*c13^2 - 9*c31^2*c04 + 32*c40*c22*c04\n",
      "c31^2*c13^2 - 3*c40*c22*c13^2 - 3*c31^2*c22*c04 + 28*c40*c31*c13*c04 - 128*c40^2*c04^2\n"
     ]
    }
   ],
   "source": [
    "#3+1\n",
    "R = PolynomialRing(QQ, var+para1+['w1']+['w2']+['v1']+['v2'])\n",
    "R.inject_variables()\n",
    "f2 = expand((w1*x1-w2*x2)^3*(v1*x1-v2*x2))\n",
    "f1 = c40*x1^4 + c31*x1^3*x2 + c22*x1^2*x2^2 + c13*x1*x2^3 + c04*x2^4\n",
    "monomials = monomial_generator(R.gens()[:n_vars], d)\n",
    "genel = [f1.coefficient(i)-f2.coefficient(i) for i in monomials]\n",
    "I2 = ideal(genel)\n",
    "elim_ideal = I2.elimination_ideal([w1,w2, v1, v2])\n",
    "num_gens = len(elim_ideal.gens())\n",
    "print(f\"{num_gens} generators: \")\n",
    "for gen in elim_ideal.gens():\n",
    "    print(gen)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 88,
   "id": "e4bca654-3085-4478-bbfe-bad60dc3b6ef",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Defining x1, x2, c40, c31, c22, c13, c04, w1, w2, v1, v2\n",
      "7 generators: \n",
      "c13^3 - 4*c22*c13*c04 + 8*c31*c04^2\n",
      "c22*c13^2 - 4*c22^2*c04 + 2*c31*c13*c04 + 16*c40*c04^2\n",
      "c31*c13^2 - 4*c31*c22*c04 + 8*c40*c13*c04\n",
      "c40*c13^2 - c31^2*c04\n",
      "c31^2*c13 - 4*c40*c22*c13 + 8*c40*c31*c04\n",
      "c31^2*c22 - 4*c40*c22^2 + 2*c40*c31*c13 + 16*c40^2*c04\n",
      "c31^3 - 4*c40*c31*c22 + 8*c40^2*c13\n"
     ]
    }
   ],
   "source": [
    "#2+2\n",
    "R = PolynomialRing(QQ, var+para1+['w1']+['w2']+['v1']+['v2'])\n",
    "R.inject_variables()\n",
    "f2 = expand((w1*x1-w2*x2)^2*(v1*x1-v2*x2)^2)\n",
    "f1 = c40*x1^4 + c31*x1^3*x2 + c22*x1^2*x2^2 + c13*x1*x2^3 + c04*x2^4\n",
    "monomials = monomial_generator(R.gens()[:n_vars], d)\n",
    "genel = [f1.coefficient(i)-f2.coefficient(i) for i in monomials]\n",
    "I2 = ideal(genel)\n",
    "elim_ideal = I2.elimination_ideal([w1,w2, v1, v2])\n",
    "num_gens = len(elim_ideal.gens())\n",
    "print(f\"{num_gens} generators: \")\n",
    "for gen in elim_ideal.gens():\n",
    "    print(gen)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "37d349fe-6367-478b-902d-a8387ed00f41",
   "metadata": {},
   "outputs": [],
   "source": [
    "#Try to find the relation between 4*4 minors of A and the condition for 2 lines"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 90,
   "id": "612cc622-e0f6-4a62-bf1b-d72ef7ac477d",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Defining c40, c31, c22, c13, c04\n"
     ]
    }
   ],
   "source": [
    "R = PolynomialRing(QQ, para1)\n",
    "R.inject_variables()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 91,
   "id": "66e9e954-5233-419f-a629-3ac1f0b0ea04",
   "metadata": {},
   "outputs": [],
   "source": [
    "I2lines31 = ideal(c22^2 - 3*c31*c13 + 12*c40*c04, c31*c22*c13 - 9*c40*c13^2 - 9*c31^2*c04 + 32*c40*c22*c04, c31^2*c13^2 - 3*c40*c22*c13^2 - 3*c31^2*c22*c04 + 28*c40*c31*c13*c04 - 128*c40^2*c04^2)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 92,
   "id": "d327849b-4d5f-47f9-a8ac-5a64cb17ec09",
   "metadata": {},
   "outputs": [],
   "source": [
    "I2lines22 = ideal(c13^3 - 4*c22*c13*c04 + 8*c31*c04^2, c22*c13^2 - 4*c22^2*c04 + 2*c31*c13*c04 + 16*c40*c04^2, c31*c13^2 - 4*c31*c22*c04 + 8*c40*c13*c04, c40*c13^2 - c31^2*c04, c31^2*c13 - 4*c40*c22*c13 + 8*c40*c31*c04, c31^2*c22 - 4*c40*c22^2 + 2*c40*c31*c13 + 16*c40^2*c04, c31^3 - 4*c40*c31*c22 + 8*c40^2*c13)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 93,
   "id": "94be6701-bc5e-4549-96e6-6977fa80b414",
   "metadata": {},
   "outputs": [],
   "source": [
    "I44minors = ideal(c31^2*c22^2 - 4*c40*c22^3 - 3*c31^3*c13 + 14*c40*c31*c22*c13 - 18*c40^2*c13^2 - 6*c40*c31^2*c04 + 16*c40^2*c22*c04, c31^2*c22*c13 - 4*c40*c22^2*c13 + 3*c40*c31*c13^2 - 9*c31^3*c04 + 32*c40*c31*c22*c04 - 48*c40^2*c13*c04, c31^2*c13^2 - 3*c40*c22*c13^2 - 3*c31^2*c22*c04 + 8*c40*c22^2*c04 + 4*c40*c31*c13*c04 - 32*c40^2*c04^2, c31*c22*c13^2 - 9*c40*c13^3 - 4*c31*c22^2*c04 + 3*c31^2*c13*c04 + 32*c40*c22*c13*c04 - 48*c40*c31*c04^2, c22^2*c13^2 - 3*c31*c13^3 - 4*c22^3*c04 + 14*c31*c22*c13*c04 - 6*c40*c13^2*c04 - 18*c31^2*c04^2 + 16*c40*c22*c04^2)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 96,
   "id": "50a4bc3e-36e7-454c-9677-d8a946665fe4",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "False"
      ]
     },
     "execution_count": 96,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "I2lines22 <= I44minors"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 98,
   "id": "9fb5e41a-9cee-4d91-980b-bfe9d3b8b4ed",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "True"
      ]
     },
     "execution_count": 98,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "I44minors <= I2lines22"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 97,
   "id": "0d00afcb-41b0-4a57-bf7e-9bab5da50628",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "False"
      ]
     },
     "execution_count": 97,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "I2lines31 <= I44minors"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 99,
   "id": "b3892d2e-5d82-41c5-89a0-9e66155481e3",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "True"
      ]
     },
     "execution_count": 99,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "I44minors <= I2lines31"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 102,
   "id": "daf8fea2-c53a-4444-9367-4114d42a949d",
   "metadata": {},
   "outputs": [],
   "source": [
    "I2lines = I2lines22.intersection(I2lines31)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 103,
   "id": "02fddae4-b33b-4229-bdaf-5eea0a2fdde5",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "True"
      ]
     },
     "execution_count": 103,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "I2lines <= I44minors"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 104,
   "id": "2a368b6f-66f3-4f5a-8cd4-fdac60609e05",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "True"
      ]
     },
     "execution_count": 104,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "I44minors <= I2lines"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 110,
   "id": "472c9d8b-6be1-4673-9cf3-c364ed0642bb",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      " 2 minimal primes\n",
      "\n",
      "\n",
      "dimension: 3\n",
      "3 generators\n",
      "c22^2 - 3*c31*c13 + 12*c40*c04\n",
      "c31*c22*c13 - 9*c40*c13^2 - 9*c31^2*c04 + 32*c40*c22*c04\n",
      "c31^2*c13^2 - 3*c40*c22*c13^2 - 3*c31^2*c22*c04 + 28*c40*c31*c13*c04 - 128*c40^2*c04^2\n",
      "\n",
      "\n",
      "dimension: 3\n",
      "7 generators\n",
      "c13^3 - 4*c22*c13*c04 + 8*c31*c04^2\n",
      "c22*c13^2 - 4*c22^2*c04 + 2*c31*c13*c04 + 16*c40*c04^2\n",
      "c31*c13^2 - 4*c31*c22*c04 + 8*c40*c13*c04\n",
      "c40*c13^2 - c31^2*c04\n",
      "c31^2*c13 - 4*c40*c22*c13 + 8*c40*c31*c04\n",
      "c31^2*c22 - 4*c40*c22^2 + 2*c40*c31*c13 + 16*c40^2*c04\n",
      "c31^3 - 4*c40*c31*c22 + 8*c40^2*c13\n"
     ]
    }
   ],
   "source": [
    "#prime decomposition\n",
    "#compute the associated primes\n",
    "all_primes = I44minors.associated_primes()\n",
    "# calculate the minimal primes\n",
    "minimal_primes = []\n",
    "for i, P in enumerate(all_primes):\n",
    "    is_minimal = True\n",
    "    for j, Q in enumerate(all_primes):\n",
    "        if i != j and P != Q and P.intersection(Q) == P:\n",
    "            is_minimal = False\n",
    "            break\n",
    "    if is_minimal:\n",
    "        minimal_primes.append(P)\n",
    "    \n",
    "print(f\"\\n {len(minimal_primes)} minimal primes\")\n",
    "\n",
    "for i, P in enumerate(minimal_primes):\n",
    "    print(\"\\n\")\n",
    "    print(f\"dimension: {P.dimension()}\")\n",
    "    print(f\"{len(P.gens())} generators\")\n",
    "    gens = P.gens()\n",
    "    for j, g in enumerate(gens):\n",
    "        print(f\"{g}\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7c70befa-4475-4ee3-9ee7-858574150a1e",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 10.7",
   "language": "sage",
   "name": "sagemath-10.7"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.13.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
